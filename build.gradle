buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    // add buildscript dependencies here if needed
  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
}

configure(subprojects) {
  apply plugin: 'java-library'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  // Generate assets.txt from assets folder
  tasks.register('generateAssetList') {
    def assetsFolder = file("${project.rootDir}/assets")
    def assetsFile = file("${assetsFolder}/assets.txt")

    inputs.dir(assetsFolder)
    doLast {
      assetsFile.delete()
      fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }
        .sort()
        .each { assetsFile.append(it + "\n") }
    }
  }
  processResources.dependsOn 'generateAssetList'

  tasks.withType(JavaCompile).configureEach {
    options.incremental = true
  }
}

subprojects {
  version = project.findProperty("projectVersion") ?: "1.0.0"
  ext.appName = 'JFallingSand'

  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
  }
}

eclipse.project.name = 'JFallingSand-parent'
